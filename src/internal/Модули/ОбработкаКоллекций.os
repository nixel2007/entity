// <Описание функции>
//
// Параметры:
//   ИсходнаяКоллекция - Структура, Массив - Коллекция, которую нужно преобразовать к коллекции сущностей.
//   РодительскаяСущность - Произвольный - Владелец коллекции
//
//  Возвращаемое значение:
//   Массив - Массив сущностей, полученных из элементов изначальной коллекции.
//
Функция ПреобразоватьКоллекциюККоллекцииСущностей(
	ИсходнаяКоллекция, 
	РодительскаяСущность,
	РодительскийОбъектМодели,
	ПодчиненнаяТаблица) Экспорт
	
	КоллекцияСущностей = Новый Массив;
	ТипКоллекции = ТипЗнч(ИсходнаяКоллекция);
	
	ОбъектМоделиЭлементКоллекции = ОбработкаКоллекций.ПолучитьОбъектМоделиДляПодчиненнойТаблицы(
		РодительскийОбъектМодели,
		ПодчиненнаяТаблица
	);

	Если ТипКоллекции = Тип("Массив") Тогда
		
		Для сч = 0 По ИсходнаяКоллекция.ВГраница() Цикл
			Значение = ИсходнаяКоллекция[сч];
			
			СущностьЭлемент = Новый СлужебнаяСущность_ЭлементКоллекцииКлючЗначение();
			
			ОбъектМоделиЭлементКоллекции.УстановитьЗначениеКолонкиВПоле(СущностьЭлемент, "ref", РодительскаяСущность);
			ОбъектМоделиЭлементКоллекции.УстановитьЗначениеКолонкиВПоле(СущностьЭлемент, "key", сч);
			ОбъектМоделиЭлементКоллекции.УстановитьЗначениеКолонкиВПоле(СущностьЭлемент, "value", Значение);
			
			КоллекцияСущностей.Добавить(СущностьЭлемент);
		КонецЦикла;
		
	ИначеЕсли ТипКоллекции = Тип("Структура") Тогда
		
		Для Каждого ЭлементКоллекции Из ИсходнаяКоллекция Цикл
			СущностьЭлемент = Новый СлужебнаяСущность_ЭлементКоллекцииКлючЗначение();
			
			ОбъектМоделиЭлементКоллекции.УстановитьЗначениеКолонкиВПоле(СущностьЭлемент, "ref", РодительскаяСущность);
			ОбъектМоделиЭлементКоллекции.УстановитьЗначениеКолонкиВПоле(СущностьЭлемент, "key", ЭлементКоллекции.Ключ);
			ОбъектМоделиЭлементКоллекции.УстановитьЗначениеКолонкиВПоле(СущностьЭлемент, "value", ЭлементКоллекции.Значение);
			
			КоллекцияСущностей.Добавить(СущностьЭлемент);
		КонецЦикла;
		
	Иначе
		ВызватьИсключение "Неизвестный тип коллекции " + ТипКоллекции;
	КонецЕсли;
	
	Возврат КоллекцияСущностей;
	
КонецФункции

Функция ПреобразоватьКоллекциюСтрокККоллекции(КоллекцияСущностей, ПодчиненнаяТаблица) Экспорт
	
	Если ПодчиненнаяТаблица.ТипТаблицы = ТипыПодчиненныхТаблиц.Массив Тогда
		Коллекция = Новый Массив;
		
		Для Каждого СущностьЭлемент Из КоллекцияСущностей Цикл
			Коллекция.Добавить(СущностьЭлемент.Получить("value"));
		КонецЦикла;
	ИначеЕсли ПодчиненнаяТаблица.ТипТаблицы = ТипыПодчиненныхТаблиц.Структура Тогда
		Коллекция = Новый Структура;
		
		Для Каждого СущностьЭлемент Из КоллекцияСущностей Цикл
			Коллекция.Вставить(СущностьЭлемент.Получить("key"), СущностьЭлемент.Получить("value"));
		КонецЦикла;
	Иначе
		ВызватьИсключение "Неизвестный тип коллекции " + ПодчиненнаяТаблица.ТипТаблицы;
	КонецЕсли;
	
	Возврат Коллекция;
	
КонецФункции

Функция ПолучитьОбъектМоделиДляПодчиненнойТаблицы(РодительскийОбъектМодели, ПодчиненнаяТаблица) Экспорт
	
	Если ПодчиненнаяТаблица.ТипТаблицы = ТипыПодчиненныхТаблиц.Массив Тогда
		ТипКолонкиКлюч = ТипыКолонок.Целое;
	ИначеЕсли ПодчиненнаяТаблица.ТипТаблицы = ТипыПодчиненныхТаблиц.Структура Тогда
		ТипКолонкиКлюч = ТипыКолонок.Строка;
	Иначе
		ВызватьИсключение "Неизвестный тип элемента " + ПодчиненнаяТаблица.ТипЭлемента;
	КонецЕсли;
	
	ОбъектМоделиЭлементКоллекции = Новый ОбъектМодели(
		Тип("СлужебнаяСущность_ЭлементКоллекцииКлючЗначение"),
		РодительскийОбъектМодели.МодельДанных()
	);

	ОбъектМоделиЭлементКоллекции.Служебный_ИмяТаблицы(ПодчиненнаяТаблица.ИмяТаблицы);
	КолонкиОбъектаМодели = ОбъектМоделиЭлементКоллекции.Служебный_Колонки();
	
	КолонкаKey = КолонкиОбъектаМодели.Найти("key", "ИмяКолонки");
	КолонкаKey.ТипКолонки = ТипКолонкиКлюч;
	
	КолонкаRef = КолонкиОбъектаМодели.Найти("ref", "ИмяКолонки");
	КолонкаRef.ТипЭлемента = РодительскийОбъектМодели.ТипСущности();
	
	КолонкаValue = КолонкиОбъектаМодели.Найти("value", "ИмяКолонки");
	Если ТипыКолонок.ЭтоПримитивныйТип(ПодчиненнаяТаблица.ТипЭлемента) Тогда
		КолонкаValue.ТипКолонки = ПодчиненнаяТаблица.ТипЭлемента;
	ИначеЕсли ТипыПодчиненныхТаблиц.Типы().Найти(ПодчиненнаяТаблица.ТипЭлемента) <> Неопределено Тогда
		ВызватьИсключение "Коллекция коллекций не поддерживается.";
	Иначе
		ОбъектМоделиТипЭлемента = РодительскийОбъектМодели.МодельДанных().Получить(ПодчиненнаяТаблица.ТипЭлемента);
		КолонкаValue.ТипКолонки = ТипыКолонок.Ссылка;
		КолонкаValue.ТипЭлемента = ПодчиненнаяТаблица.ТипЭлемента;
	КонецЕсли;
	
	Возврат ОбъектМоделиЭлементКоллекции;
	
КонецФункции