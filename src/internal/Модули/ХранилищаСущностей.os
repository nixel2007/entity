#Использовать semaphore

Перем Хранилища;
Перем Семафор;

Функция Получить(ОбъектМодели, Коннектор, МенеджерСущностей = Неопределено) Экспорт
	
	Семафор.Захватить();
	
	СвойстваКоннектора = РаботаСКоннекторами.ПолучитьСвойстваКоннектора(Коннектор);
	
	КлючХранилища = ПолучитьКлючХранилища(
		ОбъектМодели.ТипСущности(), 
		ТипЗнч(Коннектор), 
		СвойстваКоннектора.СтрокаСоединения
	);
	ХранилищеСущностей = Хранилища.Получить(КлючХранилища);
	Если ХранилищеСущностей = Неопределено Тогда
		ХранилищеСущностей = Новый ХранилищеСущностей(
			ОбъектМодели,
			ТипЗнч(Коннектор),
			СвойстваКоннектора.СтрокаСоединения, 
			СвойстваКоннектора.Параметры,
			МенеджерСущностей
		);
		Хранилища.Вставить(КлючХранилища, ХранилищеСущностей);
	КонецЕсли;
	
	Семафор.Освободить();

	Возврат ХранилищеСущностей;

КонецФункции

Процедура Закрыть(ТипКоннектора, СтрокаСоединения, ПараметрыКоннектора) Экспорт
	
	Семафор.Захватить(1000);

	ЗакрываемыеХранилища = Новый Массив;
	Для Каждого КлючИЗначение Из Хранилища Цикл
		Хранилище = КлючИЗначение.Значение;
		КоннекторХранилища = Хранилище.ПолучитьКоннектор();
		СвойстваКоннектора = РаботаСКоннекторами.ПолучитьСвойстваКоннектора(КоннекторХранилища);
		
		Если ТипКоннектора = ТипЗнч(КоннекторХранилища) И СтрокаСоединения = СвойстваКоннектора.СтрокаСоединения Тогда		
			Хранилище.Закрыть();
			ЗакрываемыеХранилища.Добавить(КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;

	Для Каждого ЗакрываемоеХранилище Из ЗакрываемыеХранилища Цикл
		Хранилища.Удалить(ЗакрываемоеХранилище);
	КонецЦикла;
	
	Семафор.Освободить();
	
КонецПроцедуры

Функция ПолучитьКлючХранилища(ТипСущности, ТипКоннектора, СтрокаСоединения)
	Возврат СтрШаблон(
		"%1 - %2 - %3",
		ТипСущности,
		ТипКоннектора,
		СтрокаСоединения
	);
КонецФункции

Хранилища = Новый Соответствие();
Семафор = Новый Семафор();
