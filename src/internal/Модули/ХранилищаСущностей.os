#Использовать semaphore

Перем Хранилища;
Перем Семафор;

Функция Получить(ОбъектМодели, Коннектор) Экспорт
	
	Семафор.Захватить();
	
	КлючХранилища = ПолучитьКлючХранилища(ОбъектМодели.ТипСущности(), ТипЗнч(Коннектор), Коннектор.ПолучитьСтрокуСоединения());
	ХранилищеСущностей = Хранилища.Получить(КлючХранилища);
	Если ХранилищеСущностей = Неопределено Тогда
		ХранилищеСущностей = Новый ХранилищеСущностей(
			ОбъектМодели, 
			ТипЗнч(Коннектор), 
			Коннектор.ПолучитьСтрокуСоединения(), 
			Коннектор.ПолучитьПараметрыКоннектора()
		);
		Хранилища.Вставить(КлючХранилища, ХранилищеСущностей);
	КонецЕсли;
	
	Семафор.Освободить();

	Возврат ХранилищеСущностей;

КонецФункции

Процедура Закрыть(ТипКоннектора, СтрокаСоединения, ПараметрыКоннектора) Экспорт
	
	Семафор.Захватить(1000);

	ЗакрываемыеХранилища = Новый Массив;
	Для Каждого КлючИЗначение Из Хранилища Цикл
		Хранилище = КлючИЗначение.Значение;
		КоннекторХранилища = Хранилище.ПолучитьКоннектор();
		Если ТипКоннектора = ТипЗнч(КоннекторХранилища) И СтрокаСоединения = КоннекторХранилища.ПолучитьСтрокуСоединения() Тогда		
			Хранилище.Закрыть();
			ЗакрываемыеХранилища.Добавить(КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЗакрываемоеХранилище Из ЗакрываемыеХранилища Цикл
		Хранилища.Удалить(ЗакрываемоеХранилище);
	КонецЦикла;
	
	Семафор.Освободить();
	
КонецПроцедуры

Функция ПолучитьКлючХранилища(ТипСущности, ТипКоннектора, СтрокаСоединения)
	Возврат СтрШаблон(
		"%1 - %2 - %3",
		ТипСущности,
		ТипКоннектора,
		СтрокаСоединения
	);
КонецФункции

Хранилища = Новый Соответствие();
Семафор = Новый Семафор();
