Перем Отборы;
Перем Сортировки;
Перем Первые;
Перем Смещение;

// Получить список отборов для выполнения поиска.
//
// Возвращаемое значение:
//   ФиксированныйМассив из ЭлементОтбора
//
Функция Отборы() Экспорт
	Возврат Новый ФиксированныйМассив(Отборы);
КонецФункции

// Добавить отбор для выполнения поиска.
//
// Параметры:
//   Поле - Строка - Имя поля для отбора.
//   ВидСравнения - ВидСравнения - Вид сравнения.
//   Значение - Произвольный - Значение для сравнения.
//
// Возвращаемое значение:
//   ОпцииПоиска - Текущий объект.
//
Функция Отбор(Поле, ВидСравнения, Значение) Экспорт

	ЭлементОтбора = Новый ЭлементОтбора(Поле, ВидСравнения, Значение);
	Отборы.Добавить(ЭлементОтбора);

	Возврат ЭтотОбъект;

КонецФункции

// Получить список сортировок для выполнения поиска.
//
// Возвращаемое значение:
//   ФиксированныйМассив из ЭлементПорядка
//
Функция Сортировки() Экспорт
	Возврат Новый ФиксированныйМассив(Сортировки);
КонецФункции

// Добавить сортировку для выполнения поиска.
//
// Параметры:
//   ИмяПоля - Строка - Имя поля для сортировки.
//   НаправлениеСортировки - НаправлениеСортировки - Направление сортировки.
//
// Возвращаемое значение:
//   ОпцииПоиска - Текущий объект.
//
Функция СортироватьПо(ИмяПоля, НаправлениеСортировки = Неопределено) Экспорт

	ЭлементПорядка = Новый ЭлементПорядка(ИмяПоля, НаправлениеСортировки);
	Сортировки.Добавить(ЭлементПорядка);

	Возврат ЭтотОбъект;

КонецФункции

// Получить количество элементов, которые необходимо выбрать.
//
// Возвращаемое значение:
//   Число - Количество элементов.
//
Функция ВыбираютсяПервые() Экспорт
	Возврат Первые;
КонецФункции

// Выбрать первые n элементов.
//
// Параметры:
//   КоличествоЭлементов - Число - Количество элементов.
//
// Возвращаемое значение:
//   ОпцииПоиска - Текущий объект.
//
Функция Первые(КоличествоЭлементов) Экспорт
	
	Первые = КоличествоЭлементов;

	Возврат ЭтотОбъект;
КонецФункции

// Получить количество элементов, которые необходимо пропустить.
//
// Возвращаемое значение:
//   Число - Количество элементов.
//
Функция ВыбираетсяСоСмещением() Экспорт
	Возврат Смещение;
КонецФункции

// Сместить выборку на n элементов.
//
// Параметры:
//   КоличествоЭлементов - Число - Количество элементов.
//
// Возвращаемое значение:
//   ОпцииПоиска - Текущий объект.
//
Функция Смещение(КоличествоЭлементов) Экспорт
	
	Смещение = КоличествоЭлементов;

	Возврат ЭтотОбъект;

КонецФункции

Процедура ПриСозданииОбъекта()
	Отборы = Новый Массив();
	Сортировки = Новый Массив();
	Первые = Неопределено;
КонецПроцедуры
